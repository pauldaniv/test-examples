buildscript {
  ext {
    moduleName = 'spark'

    springBootVersion = '2.1.+'
  }
  repositories {
    jcenter()
    mavenCentral()
    gradlePluginPortal()

  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
  }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

bootRun {
  jvmArgs = ["-Dspring.output.ansi.enabled=ALWAYS"]
}

version = '0.1-SNAPSHOT'
bootJar {
  archiveBaseName.set(moduleName)
  version = project.version
}
group = 'paul'

task unpack(type: Copy) {
  dependsOn bootJar
  from(zipTree(tasks.bootJar.outputs.files.singleFile))
  into("build/dependency")
}

docker {
  name "${project.group}/${bootJar.archiveBaseName.getOrElse(moduleName)}:${project.version}"
  copySpec.from(tasks.unpack.outputs).into("dependency")
  buildArgs(['DEPENDENCY': "dependency"])
}

repositories {
  mavenCentral()
  maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
  implementation 'org.codehaus.groovy:groovy'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation project(':common')

  compileOnly("org.apache.spark:spark-core_$apacheSparkScalaVersion:$apacheSparkVersion")
  compileOnly("org.apache.spark:spark-sql_$apacheSparkScalaVersion:$apacheSparkVersion")
  compileOnly("org.apache.spark:spark-mllib_$apacheSparkScalaVersion:$apacheSparkVersion")
  compileOnly("org.apache.spark:spark-streaming_$apacheSparkScalaVersion:$apacheSparkVersion")

  compileOnly('org.scala-lang:scala-library:2.11.8')
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
  }
}

configurations {
  // We don't want the Spark dependencies in our shadowJar.
  runtime.exclude group: 'org.apache.spark'
  all*.exclude module: 'slf4j-log4j12'
}

